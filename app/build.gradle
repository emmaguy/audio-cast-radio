apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

String readPropertyFromFile(propertyName, fileName) {
    Properties properties = new Properties()
    new File(fileName).withInputStream {
        properties.load(it)
    }
    return properties."$propertyName"
}

def gitCommitCount = "git rev-list HEAD --count".execute().text

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.emmaguy.audiocastradio"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode gitCommitCount.toInteger()
        versionName "1.0"

        resValue "string", "cast_app_id", readPropertyFromFile('castId', 'app/cast.properties')
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    // Ui
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Google Cast
    compile "com.android.support:mediarouter-v7:$support_library_version"
    compile 'com.google.android.gms:play-services-cast-framework:9.8.0'

    // RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.0.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Logging
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.7'

    // Screenshots for Firebase Test Lab
    androidTestCompile (name:'cloudtestingscreenshotter_lib', ext:'aar')

    // Espresso tests
    androidTestCompile('com.android.support.test:runner:0.6-alpha') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version") {
        exclude module: "espresso-idling-resource"
        exclude group: "com.android.support"
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espresso_version") {
        exclude module: "espresso-core"
        exclude module: "recyclerview-v7"
        exclude group: "com.android.support"
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
buildscript {
    ext.kotlin_version = '1.0.5-2'
    ext.support_library_version = '25.0.1'
    ext.espresso_version = '2.3-alpha'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}