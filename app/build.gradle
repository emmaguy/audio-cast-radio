apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

static String readPropertyFromFile(propertyName, fileName) {
    Properties properties = new Properties()
    new File(fileName).withInputStream {
        properties.load(it)
    }
    return properties."$propertyName"
}

def gitCommitCount = "git rev-list HEAD --count".execute().text

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "com.emmaguy.castradio"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode gitCommitCount.toInteger()
        versionName "1.0"

        resValue "string", "cast_app_id", readPropertyFromFile('castId', 'app/cast.properties')
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    // Ui
    implementation 'androidx.recyclerview:recyclerview:1.0.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.0.0-rc01'

    // Google Cast
    implementation 'androidx.mediarouter:mediarouter:1.0.0-alpha5'
    implementation 'com.google.android.gms:play-services-cast-framework:16.0.1'

    // Networking
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'com.squareup.moshi:moshi:1.5.0'

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.5.1'

    // Firebase (analytics & crash logging)
    implementation ('com.google.firebase:firebase-core:16.0.1') {
        exclude group: 'com.android.support'
        exclude module: 'appcompat-v7'
        exclude module: 'support-v4'
    }
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // Unit tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-alpha03'
    testImplementation 'org.mockito:mockito-core:2.13.0'

    // Espresso tests
    androidTestImplementation('androidx.test:runner:1.1.0-alpha4') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation 'com.android.support.test:rules:1.1.0-alpha4'
    androidTestImplementation("androidx.test.espresso:espresso-core:$espresso_version") {
        exclude module: "espresso-idling-resource"
        exclude group: "com.android.support"
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestImplementation("androidx.test.espresso:espresso-contrib:$espresso_version") {
        exclude module: "espresso-core"
        exclude module: "recyclerview-v7"
        exclude group: "com.android.support"
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

apply plugin: 'com.google.gms.google-services'