apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'spoon'

String readPropertyFromFile(propertyName, fileName) {
    Properties properties = new Properties()
    new File(fileName).withInputStream {
        properties.load(it)
    }
    return properties."$propertyName"
}

def gitCommitCount = "git rev-list HEAD --count".execute().text

def gitSha() {
    def p = 'git rev-parse --short HEAD'.execute([], project.rootDir)
    p.waitFor()
    if (p.exitValue() != 0) {
        throw new RuntimeException(p.errorStream.text)
    }

    return p.text.trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion '24.0.2'

    defaultConfig {
        applicationId "com.emmaguy.audiocastradio"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode gitCommitCount.toInteger()
        versionName "1.0"

        resValue "string", "cast_app_id", readPropertyFromFile('castId', 'app/cast.properties')
        buildConfigField 'String', 'GIT_SHA', "\"${gitSha()}\""

        testInstrumentationRunner "com.emmaguy.audiocastradio.AudioCastTestRunner"
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

spoon {
    adbTimeout = 60 // seconds
}

dependencies {
    // Ui
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"

    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Google Cast
    compile "com.android.support:mediarouter-v7:$support_library_version"
    compile 'com.google.android.gms:play-services-cast-framework:9.8.00'

    // RxJava
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxrelay:rxrelay:1.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7-kotlin:0.4.0'

    // Logging
    compile 'com.jakewharton.timber:timber:4.1.2'

    // Unit tests
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.7'

    // Espresso tests
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espresso_version") {
        exclude module: "espresso-idling-resource"
        exclude group: "com.android.support"
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espresso_version") {
        exclude module: "espresso-core"
        exclude module: "recyclerview-v7"
        exclude group: "com.android.support"
    }
    androidTestCompile 'com.squareup.spoon:spoon-client:1.7.0'
}
buildscript {
    ext.kotlin_version = '1.0.3'
    ext.support_library_version = '25.0.0'
    ext.espresso_version = "2.2.2"
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
    }
}

/*
Resolves dependency versions across test and production APKs, specifically, transitive
dependencies. This is required since Espresso internally has a dependency on support-annotations.
*/
configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$support_library_version"
}